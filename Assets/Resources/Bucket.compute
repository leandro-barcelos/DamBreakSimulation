#define NUM_THREADS 32
#define MAX_PARTICLES_PER_VOXEL 8

#pragma enable_d3d11_debug_symbols

// Buffers
Texture2D<float4> _FluidParticlePositionTexture;
Texture2D<float4> _WallParticlePositionTexture;
RWStructuredBuffer<uint> _Bucket;

// Parameters
float2 _FluidParticleResolution;
float2 _WallParticleResolution;
uint3 _BucketResolution;
uint _FluidParticleCount;
uint _WallParticleCount;
uint _ParticleCount;
float3 _Min;
float3 _Max;

#pragma kernel Fluid

[numthreads(NUM_THREADS, NUM_THREADS, 1)]
void Fluid(uint3 id : SV_DispatchThreadID)
{
    // Check if we're within texture bounds
    if (any(id.xy >= uint2(_FluidParticleResolution)))
        return;

    uint id_1d = id.y * uint(_FluidParticleResolution.x) + id.x;

    if (id_1d >= _FluidParticleCount)
        return;

    float3 pos = _FluidParticlePositionTexture[id.xy].xyz;
    float3 idx_3d = int3((pos - _Min) / (_Max - _Min) * (_BucketResolution - 1));
    // Ensure that idx_3d is within valid bounds
    if (any(idx_3d < 0) || any(idx_3d >= _BucketResolution))
        return;
    uint index = idx_3d.x + idx_3d.y * _BucketResolution.x + idx_3d.z * _BucketResolution.x * _BucketResolution.y;
    index *= MAX_PARTICLES_PER_VOXEL;
    for (uint j = 0; j < MAX_PARTICLES_PER_VOXEL; j++)
    {
        uint original;
        InterlockedCompareExchange(_Bucket[index + j], _ParticleCount, id_1d, original);
        if(original==_ParticleCount) break;
    }
}

#pragma kernel Wall

[numthreads(NUM_THREADS, NUM_THREADS, 1)]
void Wall(uint3 id : SV_DispatchThreadID)
{
    // Check if we're within texture bounds
    if (any(id.xy >= uint2(_WallParticleResolution)))
        return;

    uint id_1d = id.y * uint(_WallParticleResolution.x) + id.x;

    if (id_1d >= _WallParticleCount)
        return;

    id_1d += _FluidParticleCount;

    float3 pos = _WallParticlePositionTexture[id.xy].xyz;
    float3 idx_3d = int3((pos - _Min) / (_Max - _Min) * (_BucketResolution - 1));
    uint index = idx_3d.x + idx_3d.y * _BucketResolution.x + idx_3d.z * _BucketResolution.x * _BucketResolution.y;
    index *= MAX_PARTICLES_PER_VOXEL;
    for (uint j = 0; j < MAX_PARTICLES_PER_VOXEL; j++)
    {
        uint original;
        InterlockedCompareExchange(_Bucket[index + j], _ParticleCount, id_1d, original);
        if(original==_ParticleCount) break;
    }
}

#pragma kernel ClearBucket

[numthreads(10, 10, 10)]
void ClearBucket(uint3 id : SV_DispatchThreadID)
{
    // Check if we're within texture bounds
    if (any(id >= _BucketResolution))
        return;

    uint idx = id.x + id.y * _BucketResolution.x + id.z * _BucketResolution.x * _BucketResolution.y;
    idx *= MAX_PARTICLES_PER_VOXEL;
    for (uint j = 0; j < MAX_PARTICLES_PER_VOXEL; j++)
    {
        _Bucket[idx + j] = _ParticleCount;
    }
}
